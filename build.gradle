plugins {
	id 'dev.architectury.loom' version '1.4-SNAPSHOT'
	id 'maven-publish'
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
	}
	maven {
		name = "jitpack.io"
		url = "https://jitpack.io"
	}
	jcenter()
	mavenLocal()
}

loom {

}

configurations {
	jarLibs
	implementation.extendsFrom jarLibs
}

afterEvaluate {
	loom.runs.configureEach {
		// https://fabricmc.net/wiki/tutorial:mixin_hotswaps
		vmArg "-javaagent:${ configurations.compileClasspath.find { it.name.contains("sponge-mixin") } }"
	}
}

ext {
	worldmap_version = "1.37.2"
	minimap_version = "23.9.3"
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	jarLibs 'com.github.ben-manes.caffeine:caffeine:3.1.8'
	jarLibs "com.github.collarmc:pounce:0e8492b68e"
	jarLibs 'org.xerial:sqlite-jdbc:3.44.1.0'

	modImplementation "maven.modrinth:xaeros-world-map:${worldmap_version}_Fabric_1.20"
	modImplementation "maven.modrinth:xaeros-minimap:${minimap_version}_Fabric_1.20"
	modCompileOnly files("libs/BetterPvP_${minimap_version}_Fabric_1.20.jar")
	modImplementation files("libs/baritone-api-fabric-1.20.1-elytra-beta-v1.jar")
	modImplementation "maven.modrinth:waystones:14.0.2+fabric-1.20"
	modImplementation "maven.modrinth:balm:7.1.4+fabric-1.20.1"
	modCompileOnly "maven.modrinth:fwaystones:3.1.3+mc1.20"
	modRuntimeOnly "maven.modrinth:owo-lib:0.11.2+1.20"
	runtimeOnly "blue.endless:jankson:1.2.3"
	modRuntimeOnly "maven.modrinth:auth-me:7.0.2+1.20"
	modRuntimeOnly "maven.modrinth:cloth-config:11.1.106+fabric"
}

processResources {
	filesMatching("fabric.mod.json") {
		expand([
				"version": project.version,
				"worldmap_version": project.ext.worldmap_version,
				"minimap_version": project.ext.minimap_version
		])
	}
}

tasks.register('printWorldMapVersion') {
	println project.ext.worldmap_version
}
tasks.register('printMinimapVersion') {
	println project.ext.minimap_version
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

shadowJar {
	configurations = [project.configurations.jarLibs]
	archiveClassifier.set('shadow')

	exclude 'com/google/**'
	exclude 'org/objectweb/**'
	exclude 'org/checkerframework/**'
	exclude 'org/sqlite/native/FreeBSD/**'
	exclude 'org/sqlite/native/Linux-Android/**'
	exclude 'org/sqlite/native/Linux-Musl/**'
	exclude 'org/sqlite/native/Linux/arm/**'
	exclude 'org/sqlite/native/Linux/aarch64/**'
	exclude 'org/sqlite/native/Linux/armv6/**'
	exclude 'org/sqlite/native/Linux/x86/**'
	exclude 'org/sqlite/native/Linux/armv7/**'
	exclude 'org/sqlite/native/Linux/ppc64/**'
	exclude 'org/sqlite/native/Windows/armv7/**'
	exclude 'org/sqlite/native/Windows/aarch64/**'
	exclude 'org/sqlite/native/Windows/armv7/**'
	exclude 'org/slf4j/**'
	relocate "com.collarmc.pounce", "xaeroplus.com.collarmc.pounce.shadow"
	relocate "com.github.benmanes.caffeine", "xaeroplus.com.github.benmanes.caffeine.shadow"
}

remapJar {
	dependsOn shadowJar
	inputFile.set shadowJar.archiveFile.get()
}
